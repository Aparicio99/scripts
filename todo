#!/usr/bin/awk -f

BEGIN {
	file = ENVIRON["HOME"]"/todo.txt"

	while (getline < file) {
		data[++n] = $0
	}

	write = 1
	switch(ARGV[1]) {
		case "":
			write = 0
			break

		case "add":
			data[++n] = ARGV[2]
			break

		case "rm":
			delete data[ARGV[2]]
			break

		case "update":
			data[ARGV[2]] = ARGV[3]
			break

		case "cat":
			data[ARGV[2]] = data[ARGV[2]] " " ARGV[3]
			break

		case "swap":
			tmp = data[ARGV[2]]
			data[ARGV[2]] = data[ARGV[3]]
			data[ARGV[3]] = tmp
			break

		case "mv":
			txt = data[ARGV[2]]
			delete data[ARGV[2]]
			for(i = ++n; i > ARGV[3]; i--)
					data[i] = data[i-1]
			data[ARGV[3]] = txt
			break

		default:
			write = 0
			print "wut?"
	}

	format["\\*\\*"] = "\033[0;1m"  # bold
	format["__"]     = "\033[0;4m"  # underline
	format["##"]     = "\033[0;7m"  # block
	format["!!"]     = "\033[0;31m" # red
	format["%%"]     = "\033[0;32m" # green
	format["\\$\\$"] = "\033[0;33m" # yellow

	j = 1
	for (i = 1; i <= n; i++)
		if (data[i]) {
			s = data[i]
			for (token in format) {
				s = gensub(token "(.*)" token, format[token] "\\1\033[0;0m", "g", s)
				s = gensub(token "(.*)", format[token] "\\1\033[0;0m", "g", s)
			}
			print j++ ")", s
			if (write)
				print data[i] > file
		}

}
