#!/usr/bin/awk -f
BEGIN {
	ORS = "\r\n"
	sock = "/inet/tcp/0/spaceflight1.nasa.gov/http"
	print "GET /realdata/sightings/cities/rss/index.cgi?country=Portugal&region=None&city=Lisbon HTTP/1.1" |& sock
	print "Host: spaceflight1.nasa.gov" |& sock
	print "" |& sock

	while (sock |& getline && $1 != "<?xml");

	while (sock |& getline && $1 != 0) {
		sub("&#176;", "ยบ", $0)
		sub("&lt.*", "", $0)
		gsub(",", "", $0)
		switch($1) {
			case /<title>/:
				sub(".*<title>", "", $0)
				sub("</title>", "", $0)
				date = $1
				title = $0
				break
			case "Date:":
				dayw = substr($2, 0, 3)
				break
			case "Time:":
				time = $2
				apm = $3
				break
			case "Duration:":
				sub(".*Duration: ", "", $0)
				dur = $0
				break
			case "Maximum":
				max = $3
				break
			case "Approach:":
				sub(".*Approach: ", "", $0)
				app = $0
				break
			case "Departure:":
				sub(".*Departure: ", "", $0)
				dep = $0
				break
			case "</item>":
				s = sprintf("%s %s   %s %s      %-10s %-20s %-20s %s", dayw, title, time, apm, max, app, dep, dur);

				if (apm == "AM")
					s = "\033[38;5;240m"s"\033[0m" # gray
				else if (date == strftime("%Y-%m-%d", systime()))
					s = "\033[38;5;46m"s"\033[0m" # green

				print s
				break
		}
	}
}
