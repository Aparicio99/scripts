#!/usr/bin/awk -f

function readval(file) {
	getline line < (path file)
	return line
}

BEGIN {
	path = "/sys/class/power_supply/BAT0/"

	status        = readval("status")
	capacity      = readval("capacity")
	charge_now    = readval("charge_now") / 1000
	charge_full   = readval("charge_full") / 1000
	current_now   = readval("current_now") / 1000
	voltage_now   = readval("voltage_now") / 1000000

	power_usage   = (current_now / 1000) * voltage_now

	printf "Status: %s\n", status
	printf "Charge: %d% (%d/%d mAh)\n", capacity, charge_now, charge_full
	printf "Current: %d mA @ %.3f V\n", current_now, voltage_now
	printf "Power usage: %.2f W\n", power_usage

	if (status == "Discharging")
		time_left    = charge_now / current_now

	else if (status == "Charging")
		time_left    = (charge_full - charge_now) / current_now

	hours_left   = int(time_left)
	minutes_left = int((time_left - hours_left) * 60)
	printf "Time left: %dh%02dm\n", hours_left, minutes_left


}
